plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagement}"
    id "org.openapi.generator" version "${openApiGeneratorVersion}"
}

openApiGenerate {
    generatorName = "spring"
    library = 'spring-mvc'
    inputSpec = "$rootProject.projectDir/server/src/main/resources/api/api.yaml".toString()
    outputDir = "$buildDir".toString()
    apiPackage = "${group}.${rootProject.name}.api"
    invokerPackage = "${group}.${rootProject.name}.invoker"
    modelPackage = "${group}.${rootProject.name}.model"
    modelFilesConstrainedTo = []
    configOptions = [
            delegatePattern: "true",
            dateLibrary: "java11"
    ]
}

// Generate code from swagger before compiling this project
compileJava.dependsOn tasks.openApiGenerate
// Add generated code as additional source code directory for this project
sourceSets.main.java.srcDirs += "${buildDir}/src/main/java"
bootJar {
    enabled = false
}
jar {
    archivesBaseName = "${rootProject.name}-${project.name}"
    enabled = true
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "io.springfox:springfox-swagger2:${springfoxSwaggeVersion}"
    implementation "io.springfox:springfox-swagger-ui:${springfoxSwaggeVersion}"
    implementation "org.openapitools:openapi-generator:${openApiGeneratorVersion}"
    implementation "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullable}"
}
