plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id "org.openapi.generator" version "${openApiGeneratorVersion}"
}

openApiGenerate {
    generatorName = "spring"
    library = 'spring-boot'
    inputSpec = "$rootProject.projectDir/server/src/main/resources/api/api.yaml".toString()
    outputDir = "$buildDir".toString()
    apiPackage = "${group}.${rootProject.name}.api"
    modelPackage = "${group}.${rootProject.name}.model"
    //https://github.com/OpenAPITools/openapi-generator/pull/10581
    modelFilesConstrainedTo = []
    configOptions = [
            useTags: "true",
            delegatePattern: "true",
            dateLibrary    : "java11"
    ]
}

// Delete swagger2 files
task deleteFiles(type: Delete) {
    delete "${buildDir}/src/main/java/org/openapitools"
}

// Generate code from swagger before compiling this project
compileJava.dependsOn tasks.deleteFiles
deleteFiles.dependsOn tasks.openApiGenerate
// Add generated code as additional source code directory for this project
sourceSets.main.java.srcDirs += "${buildDir}/src/main/java"
bootJar {
    enabled = false
}
jar {
    archivesBaseName = "${rootProject.name}-${project.name}"
    enabled = true
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "org.openapitools:openapi-generator:${openApiGeneratorVersion}"
    implementation "org.openapitools:jackson-databind-nullable:${jacksonDatabindNullableVersion}"

    testImplementation "org.hibernate:hibernate-validator:${hibernateValidatorVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}
